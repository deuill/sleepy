# Maintainer: Alex Palaistras <alex[at]deuill[dot]org>
pkgname=sleepy
pkgver=0.5.0
pkgrel=1
pkgdesc="Sleepy is a lightweight web framework."
url="http://deuill.org/sleepy"
arch=('x86_64' 'i686')
license=('MIT')
install='sleepy.install'
makedepends=('go' 'sqlite')
optdepends=(
	'mysql: For the database module'
	'memcached: For caching oft-used requests'
)
backup=(
	'etc/sleepy/sleepy.conf'
	'etc/sleepy/modules.d/auth.conf'
	'etc/sleepy/modules.d/database.conf'
	'etc/sleepy/modules.d/email.conf'
	'etc/sleepy/modules.d/file.conf'
	'etc/sleepy/modules.d/image.conf'
	'etc/sleepy/modules.d/template.conf'
	'etc/sleepy/modules.d/user.conf'
	'var/lib/sleepy/sleepy.db'
)

prepare() {
	rm -f ${srcdir}/${pkgname}-${pkgver}
	ln -sf ../../../.. ${srcdir}/${pkgname}-${pkgver}
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install install-data
	chown -R http:http "${pkgdir}/var/lib/sleepy"
	chmod -R go-rwx "${pkgdir}/var/lib/sleepy"
	install -Dm 644 data/init/systemd/sleepy.conf "${pkgdir}/usr/lib/tmpfiles.d/sleepy.conf"
	install -Dm 644 data/init/systemd/sleepy.service "${pkgdir}/usr/lib/systemd/system/sleepy.service"
}

# vim:set ts=4 sw=4 et:
